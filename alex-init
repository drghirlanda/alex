#!/usr/bin/pebl

define Start( par ) {

  ## This is the folder where we look for support files:
  gAlexDir <- "Library/Init/"
  ## It can itself be in a number of places:
  dirs <- [ "",                      # Current folder takes precedence
            "/usr/share/alex/",      # Linux
                                     # ADD OS X
            "C:\Program Files\alex\" # Windows
          ]
  ## Here we find out where:
  found <- 0
  loop( gD, dirs ) {
    if( IsDirectory( gD + gAlexDir ) ) {
      gAlexDir <- gD + gAlexDir
      found <- 1
      Print( gAlexDir + " found!" )
      break
    } else {
      Print( gD + gAlexDir + " not found, moving on" )
    }
  }
  if( not found ) {
    SignalFatalError( "Cannot find "+gAlexDir+" anywhere" )
  }

  # Experiment name:
  gExpDir <- Nth(par, 1)
  if( IsDirectory( gExpDir ) ) {
    SignalFatalError( "Folder "+gExpDir+" already exists, stopping" )
  } 
  gExpDir <- gExpDir + "/"

  Print( "Creating experiment in " + gExpDir )
  MakeDirectory( gExpDir )
  loop( f, GetDirectoryListing( gAlexDir ) ) {
    MakeDirectory( gExpDir + f )
  }

  loop( f, GetDirectoryListing( gAlexDir + "Design" ) ) {
    if( f != "." and f != ".." ) {
      source <- gAlexDir + "Design/" + f
      destination <- gExpDir + "Design/" + f
      AppendFile( destination, source )
    }
  }

  loop( f, GetDirectoryListing( gAlexDir + "Materials" ) ) {
    if( f != "." and f != ".." ) {
      source <- gAlexDir + "Materials/" + f
      destination <- gExpDir + "Materials/" + f
      AppendFile( destination, source )
    }
  }

  if( GetSystemType() == "WINDOWS" ) {
    runExp <- gExpDir + "/RunExperiment.bat"
    alexbat <- gD + "alex.bat"
    AppendFile( runExp, alexbat )
  } else { # Linux and Mac OS X
    file <- FileOpenWrite( gExpDir + "/RunExperiment.sh" )
    FilePrint( file, "#!/bin/sh" )
    FilePrint( file, "alex" )
    FileClose( file )
    SystemCall( "chmod u+x "+gExpDir+"/RunExperiment.sh" )
  }  
}