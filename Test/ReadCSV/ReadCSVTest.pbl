## These functions modify pebl's stock ReadCSV to handle CSV files
## whose last line does not terminate in a newline (this happens when
## saving CSV in some spreadsheet software)

define Start( par ) {
  filename <- "CSVTest.csv"
  Print( "Reading " + filename + ":" )
  table <- ReadCSV( filename )
  i <- 1
  loop( line, table ) {
    Print_( i + ": " )
    loop( field, line ) {
      Print_( "*" + field + "* / " )
    }
    Print( "End of line" )
    i <- i + 1
  }
}

## This function does the heavy lifting and works based on the fact
## that pebl's FileReadLine() returns also incomplete lines. The
## algorithm to parse a CSV line with quotes is copied from pebl's
## ReadCSV function.
define ParseCSVLine( rawline ) {
  parsedLine <- [] # List of tokens parsed from the CSV line
  token <- ""      # The token being currently parsed 
  inquote <- 0     # 1 if we are inside a quoted token, 0 otherwise

  n <- StringLength( rawline )

  if( n>0 ) {
    loop( i, Sequence(1,n,1) ) {
      char <- SubString( rawline, i, 1 )
      if( char == gQuote and (not inquote) ) { # Entering quoted token
        inquote <- 1
        token <- token + char
      } elseif( char == gQuote and inquote ) { # Exiting quoted token
        inquote <- 0
        token <- token + char
      } elseif( char == "," and (not inquote) ) { # Token is ready
        PushOnEnd( parsedLine, StripQuotes( StripSpace( token ) ) )
        token <- ""
      } else {
        token <- token + char
      }
    }
    if( StringLength(token) ) { # Leftovers?
      PushOnEnd( parsedLine, StripQuotes( StripSpace( token ) ) )
    }
  }
  return parsedLine
}

define ReadCSV(filename)
{
  if( not FileExists( filename ) ) {
    SignalFatalError( "Cannot find file: "+ filename )
  }

  lines <- FileReadList(filename)
  table <- [] 

  loop( line, lines ) { 
    parsedLine <- ParseCSVLine( line )
    if( Length( parsedLine ) ) {
      PushOnEnd( table, parsedLine )
    }
  }

  return table
}

